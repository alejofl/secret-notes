def app_version = ""
def frontend_image = null
def backend_image = null
def tests_port = ""
def remote = [:]
remote.allowAnyHosts = true

pipeline {
    agent any

    environment {
        IMAGE_NAME = "alejofl/secret-notes"
        VITE_API_BASE_URL = credentials('VITE_API_BASE_URL')
        VITE_PUBLIC_POSTHOG_KEY = credentials('VITE_PUBLIC_POSTHOG_KEY')
        VITE_PUBLIC_POSTHOG_HOST = credentials('VITE_PUBLIC_POSTHOG_HOST')
        REMOTE_HOST = credentials('REMOTE_HOST')
    }

    tools {
        nodejs "node-22"
    }

    stages {
        stage('Preparation') {
            steps {
                checkout scm
                script {
                    app_version = sh(
                        script: "grep '\"version\"' package.json | sed 's/.*\"version\": \"\\(.*\\)\".*/\\1/'",
                        returnStdout: true
                    ).trim()
                }
                echo "App Version: ${app_version}"
            }
        }

        stage('Linting') {
            parallel {
                stage('Static Code Analysis (SonarQube)') {
                    steps {
                        script {
                            def scanner_home = tool 'SonarScanner';
                            withSonarQubeEnv('SonarScanner') {
                                sh "${scanner_home}/bin/sonar-scanner"
                            }
                        }
                    }
                }
                stage('Security Scan (Snyk)') {
                    steps {
                        snykSecurity(
                            snykInstallation: 'SnykScanner',
                            snykTokenId: 'snyk-token',
                        )
                    }
                }
            }
        }

        stage('Unit Testing') {
            parallel {
                stage('Backend Unit Testing') {
                    steps {
                       sh 'cd backend && npm install && npm run test && cd ..'
                    }
                }
                stage('Frontend Unit Testing') {
                    steps {
                       sh 'cd frontend && npm install && npm run test && cd ..'
                    }
                }
            }
        }

        stage('Build') {
            when { branch 'deploy' }
            parallel {
                stage('Frontend Build') {
                    steps {
                        script {
                            def build_args = [
                                "VITE_API_BASE_URL=${env.VITE_API_BASE_URL}",
                                "VITE_PUBLIC_POSTHOG_KEY=${env.VITE_PUBLIC_POSTHOG_KEY}",
                                "VITE_PUBLIC_POSTHOG_HOST=${env.VITE_PUBLIC_POSTHOG_HOST}",
                            ]
                            frontend_image = docker.build("${env.IMAGE_NAME}-frontend:latest", "--build-arg ${build_args.join(' --build-arg ')} frontend")
                        }
                    }
                }
                stage('Backend Build') {
                    steps {
                        script {
                            backend_image = docker.build("${env.IMAGE_NAME}-backend:latest", "backend")
                        }
                    }
                }
            }
        }

        stage('Delivery') {
            when { branch 'deploy' }
            parallel {
                stage('Frontend Delivery') {
                    steps {
                        script {
                            docker.withRegistry('https://registry.hub.docker.com', 'dockerhub') {
                                frontend_image.push("${app_version}")
                                frontend_image.push("latest")
                            }
                        }
                    }
                }
                stage('Backend Delivery') {
                    steps {
                        script {
                            docker.withRegistry('https://registry.hub.docker.com', 'dockerhub') {
                                backend_image.push("${app_version}")
                                backend_image.push("latest")
                            }
                        }
                    }
                }
            }
        }

        stage('Deploy') {
            when { branch 'deploy' }
            steps {
                script {
                    withCredentials([sshUserPrivateKey(credentialsId: 'ssh-key', keyFileVariable: 'identity', passphraseVariable: '', usernameVariable: 'username')]) {
                        remote.name = env.REMOTE_HOST
                        remote.host = env.REMOTE_HOST
                        remote.user = username
                        remote.identityFile = identity
                        tests_port = sshCommand remote: remote, command: "cd app && ./start-blue-green.sh"
                    }
                }
                sh "cd frontend && npm install && TESTS_URL='http://${REMOTE_HOST}:${tests_port}' npm run e2e && cd .."
                sh "cd frontend && TESTS_URL='http://${REMOTE_HOST}:${tests_port}' npm run performance && cd .."
                script {
                    withCredentials([sshUserPrivateKey(credentialsId: 'ssh-key', keyFileVariable: 'identity', passphraseVariable: '', usernameVariable: 'username')]) {
                        remote.name = env.REMOTE_HOST
                        remote.host = env.REMOTE_HOST
                        remote.user = username
                        remote.identityFile = identity
                        sshCommand remote: remote, command: "cd app && ./switch-blue-green.sh"
                    }
                }
            }
        }

        stage('Cleanup') {
            when { branch 'deploy' }
            steps {
                sh 'docker image prune -a -f'
            }
        }
    }

    post {
         always {
            script {
                if (currentBuild.currentResult == 'FAILURE') { 
                    emailext subject: '$DEFAULT_SUBJECT',
                        body: '$DEFAULT_CONTENT',
                        to: '$DEFAULT_RECIPIENTS'
                }
            }
        }
    }
}